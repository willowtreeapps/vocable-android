plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id "androidx.navigation.safeargs.kotlin"
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'com.willowtree.vocable'
    compileSdk 33
    defaultConfig {
        applicationId "com.willowtree.vocable"
        minSdkVersion 24
        targetSdkVersion 33
        //30 was the last versionCode used on Play Store before implementing CI with github actions
        versionCode Integer.valueOf(System.getenv("VERSION_CODE") ?: 1) + 30
        versionName System.getenv("VERSION_NAME") ?: "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        release {
            storeFile rootProject.file("keystore")
            storePassword System.getenv("RELEASE_KEY_PASSWORD")
            keyAlias System.getenv("RELEASE_KEY_ALIAS")
            keyPassword System.getenv("RELEASE_KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            buildConfigField("boolean", "USE_HEAD_TRACKING", "true")
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            def useHeadTracking = project.getProperties().getOrDefault("USE_HEAD_TRACKING", "true")
            buildConfigField("boolean", "USE_HEAD_TRACKING", useHeadTracking)
        }
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    viewBinding {
        enabled = true
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    useLibrary 'android.test.runner'

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    // Provides ARCore Session and related resources.
    implementation 'com.google.ar:core:1.30.0'
    implementation 'com.google.ar.sceneform.ux:sceneform-ux:1.17.1'

    def coroutines_version = "1.7.3"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.0.3'

    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'android.arch.lifecycle:viewmodel:1.1.1'

    implementation 'io.insert-koin:koin-core:3.1.5'
    implementation 'io.insert-koin:koin-android:3.1.5'

    implementation "androidx.room:room-runtime:2.5.2"
    kapt "androidx.room:room-compiler:2.5.2"
    implementation "androidx.room:room-ktx:2.5.2"

    implementation 'androidx.security:security-crypto:1.0.0-beta01'

    implementation "androidx.navigation:navigation-fragment-ktx:2.4.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.1"

    implementation 'com.vmadalin:easypermissions-ktx:1.0.0'

    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.firebase:firebase-analytics:20.1.2'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.2.9'

    // Moshi
    implementation 'com.squareup.moshi:moshi-kotlin:1.12.0'

    androidTestUtil 'androidx.test:orchestrator:1.4.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    androidTestImplementation "androidx.room:room-testing:2.2.5"
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:2.3.1"
}
